import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-gulp-plugin:0.11'
    }
}

apply plugin: 'com.moowork.gulp'

task bowerInstall(type: Exec, dependsOn: "npmInstall") {
    description("Install runtime dependencies.")
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'node_modules\bower\bin\bower', 'install'
    } else {
        commandLine './node_modules/bower/bin/bower', 'install'
    }
}

task setup(dependsOn: ["nodeSetup", "npmInstall", "installGulp", "bowerInstall"]) {
    description("Install build dependencies.")
}

task test(dependsOn: ["setup", "gulp_test"]) {
    description("Run 'gulp test'")
}
gulp_test.mustRunAfter setup

task build(dependsOn: ["setup", "gulp_build"]) {
    description("Run 'gulp build'")
}
gulp_build.mustRunAfter setup

gulp {
    // Set the directory where gulpfile.js should be found
    workDir = file("${project.projectDir}")
    colors = true
    bufferOutput = false
}

node {
    version = '4.2.2'
    download = true
}
